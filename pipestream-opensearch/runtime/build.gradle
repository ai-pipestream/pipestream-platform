plugins {
    alias(libs.plugins.java.library)
    alias(libs.plugins.quarkus.extension)
    alias(libs.plugins.proto.toolchain)
}

description = 'Pipestream OpenSearch Extension - Runtime'

quarkusExtension {
    deploymentModule = 'pipestream-opensearch-deployment'
}

// ============================================================
// PROTOBUF CONFIGURATION - OpenSearch gRPC APIs (Mutiny stubs)
// ============================================================

pipestreamProtos {
    sourceMode = 'git-proto-workspace'

    gitRepo = "https://github.com/opensearch-project/opensearch-protobufs.git"
    gitRef = "1.2.0"

    modules {
        register("opensearch-protobufs") {
            gitSubdir = "protos"  // Only protos/ - excludes tools/ test fixtures
        }
    }
}

// ============================================================
// END PROTOBUF CONFIGURATION
// ============================================================

dependencies {
    // Quarkus BOM provides platform properties
    implementation platform(libs.quarkus.bom)

    // Quarkus core
    implementation 'io.quarkus:quarkus-core'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-mutiny'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-grpc'  // Required for generated Mutiny stubs (MutinyStub, etc.)

    // OpenSearch Java Client
    implementation libs.opensearch.java
    implementation libs.httpclient5

    // gRPC and Protobuf runtime (generated code depends on these)
    implementation libs.grpc.stub
    implementation libs.grpc.protobuf
    implementation libs.protobuf.java

    // Dynamic gRPC for OpenSearch gRPC client
    api project(':quarkus-dynamic-grpc')

    // Annotation processor for extension metadata
    annotationProcessor 'io.quarkus:quarkus-extension-processor'

    // Test dependencies
    testImplementation 'io.quarkus:quarkus-test-common'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.hamcrest:hamcrest'

    // Gradle 9 requires explicit JUnit Platform launcher
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events 'passed', 'failed', 'skipped'
    }
}

configurations.all {
    exclude group: 'io.quarkus', module: 'quarkus-virtual-threads'
    exclude group: 'io.quarkus', module: 'quarkus-virtual-threads-deployment'
}

// Workaround for Gradle 9: disable validateExtension which performs unsafe configuration resolution
tasks.matching { it.name == 'validateExtension' }.configureEach {
    enabled = false
}
