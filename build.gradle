plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'ai.pipestream'

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/ai-pipestream/*')
            credentials {
                username = findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = rootProject.group

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    tasks.withType(Test).configureEach {
        systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'
    }
}

// Wrapper task for Gradle wrapper
wrapper {
    gradleVersion = '8.12'
    distributionType = Wrapper.DistributionType.ALL
}
